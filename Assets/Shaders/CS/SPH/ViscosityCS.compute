#pragma kernel Viscosity

#include "Common.hlsl"

StructuredBuffer<Particle> _ParticleBufferRead;
StructuredBuffer<ParticleDensity> _ParticleDensityBufferRead;
RWStructuredBuffer<ParticleForce> _ParticleForceBuffer;


[numthreads(128,1,1)]
void Viscosity(uint3 id : SV_DispatchThreadID)
{
    RETURN_IF_INVALID(id);

    const uint p_id = id.x;
	Particle p = _ParticleBufferRead[p_id];

	float3 force = _ParticleForceBuffer[p_id].linearForce;
	if(p.IsActive())
	{
		const float h_sq = pow(_H, 2);

		float3 p_pos = p.pos;
		float3 P_velocity = p.vel;
		float  P_density  = _ParticleDensityBufferRead[p_id].density;
		
		FOR_EACH_NEIGHBOR_START(p_pos, n_id, _GridBuffer)
		{
			Particle np = _ParticleBufferRead[n_id];
			if(!np.IsActive()) continue;

			float3 n_pos = np.pos;

			float3 r = p_pos - n_pos;
			float r_sq = dot(r, r);
			if (r_sq < h_sq && p_id != n_id)
			{
				float3 N_velocity = np.vel;
				float  N_density = _ParticleDensityBufferRead[n_id].density;

				// Viscosity Term
				force += CalculateLapVelocity(_H, _ParticleMass, _Viscosity, r, P_velocity, N_velocity, P_density, N_density);

			}
		}
		FOR_EACH_NEIGHBOR_END
	}


	_ParticleForceBuffer[p_id].linearForce = force;
}